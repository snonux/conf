use Rex -feature => ['1.14', 'exec_autodie'];
use Rex::Logger;

our $HOME = $ENV{HOME};
our $DOT = "$HOME/git/rexfiles/dotfiles";

desc 'Install packages on Fedora Linux';
task 'pkg_fedora', sub {
  pkg 'tig', ensure => 'installed';
};

desc 'Install ~/.config/helix';
task 'home_helix', sub {
  my ($dst, $src) = ("$HOME/.config/helix", "$DOT/helix");
  file $dst, ensure => 'directory';
  file "$dst/" . basename($_), ensure => 'present', source => $_  for glob "$src/*"; 
};

# TODO: Maybe put into a more generic sub, so it can be re-used by other tasks.
# DODO: Another method to reverse the install. G.g., copy from ~ into rexfiles/dotfiles.
task 'helix_diff', sub {
  my ($dst, $src) = ("$HOME/.config/helix", "$DOT/helix");
  for (glob "$src/*") {
    my $dst_file = $dst . '/' . basename $_;
    if (-f $dst_file) {
      local $, = "\n";
      say split '\n', run "diff -u $_ $dst_file";
    } else {
      Rex::Logger::info("$dst_file not there, would install it");
    }
  }
};

desc 'Install ~/scripts';
task 'home_scripts', sub {
  my ($dst, $src) = ("$HOME/scripts", "$DOT/scripts");
  file $dst, ensure => 'directory';
  file "$dst/" . basename($_), ensure => 'present', source => $_,  mode => '0755'
    for glob "$src/*"; 
};

desc 'Install ~/.ssh files';
task 'home_ssh', sub {
  file "$HOME/.ssh", ensure => 'directory', mode => '0700';
  file "$HOME/.ssh/config", ensure => 'present', source => "$DOT/ssh/config", mode => '0600';
};

desc 'Install all my ~ files';
task 'home', sub {
  home_helix();
  home_scripts();
  home_ssh();
};