use Rex -feature => ['1.14', 'exec_autodie'];
use Rex::Logger;

our $HOME = $ENV{HOME};
our $DOT = "$HOME/git/rexfiles/dotfiles";

sub install_dir {
  my ($src_glob, $dst_dir, $file_mode) = @_;

  file $dst_dir, ensure => 'directory', mode => '0700';

  file "$dst_dir/" . basename($_),
    ensure => 'present',
    source => $_,
    mode => $file_mode // '0640'
      for glob $src_glob;
}

sub install_file {
  my ($src_file, $dst_file, $file_mode) = @_;

  say "Installing $src_file to $dst_file";
  file $dst_file, ensure => 'present',
    source => $src_file,
    mode => $file_mode // '0640';
}

sub install {
  my ($src, $dst, $mode) = @_;
  ($dst =~ /\/$/ ? \&install_dir : \&install_file)->($src, $dst, $mode);
}

desc 'Install packages on Fedora Linux';
task 'pkg_fedora', sub {
  pkg 'tig', ensure => 'installed';
};

desc 'Install ~/.config/helix';
task 'home_helix', sub { install "$DOT/helix/*" => "$HOME/.config/helix/" };

desc 'Install ~/scripts';
task 'home_scripts', sub { install "$DOT/scripts/*" => "$HOME/scripts/", '0750' };

desc 'Install ~/.ssh files';
task 'home_ssh', sub { install "$DOT/ssh/config" => "$HOME/.ssh/config", '0600' };

desc 'Vale and proselint';
task 'home_vale', sub {
  install "$DOT/vale.ini" => "$HOME/.vale.ini";
  say 'Now you can run "vale sync"';
};

desc 'Install all my ~ files';
task 'home', sub {
  home_helix();
  home_scripts();
  home_ssh();
  home_vale();
};
