NAMESPACE := "services"
APP_NAME := "anki-sync-server"

# Build Docker image
build:
    just -f docker-image-Justfile build

# Push image to registry
push:
    just -f docker-image-Justfile push

# Build and push
build-push: build push

# Show deployment status
status:
    @echo "=== Pods ==="
    @kubectl get pods -n {{NAMESPACE}} | grep anki
    @echo ""
    @echo "=== Service ==="
    @kubectl get svc -n {{NAMESPACE}} anki-sync-server-service
    @echo ""
    @echo "=== Ingress ==="
    @kubectl get ingress -n {{NAMESPACE}} anki-sync-server-ingress
    @echo ""
    @echo "=== PVC ==="
    @kubectl get pvc -n {{NAMESPACE}} anki-data-pvc
    @echo ""
    @echo "=== ArgoCD Status ==="
    @kubectl get application {{APP_NAME}} -n cicd -o jsonpath='Sync: {.status.sync.status}, Health: {.status.health.status}' 2>/dev/null && echo "" || echo "Not found"

# View logs
logs lines="100":
    kubectl logs -n {{NAMESPACE}} -l app=anki-sync-server --tail={{lines}} -f

# Port forward
port-forward port="8080":
    @echo "Forwarding anki-sync-server to localhost:{{port}}"
    kubectl port-forward -n {{NAMESPACE}} svc/anki-sync-server-service {{port}}:8080

# Trigger ArgoCD sync
sync:
    @echo "Triggering ArgoCD sync..."
    @kubectl annotate application {{APP_NAME}} -n cicd argocd.argoproj.io/refresh=normal --overwrite
    @sleep 2
    @kubectl get application {{APP_NAME}} -n cicd -o jsonpath='Sync: {.status.sync.status}, Health: {.status.health.status}' && echo ""

# Show ArgoCD application details
argocd-status:
    argocd app get {{APP_NAME}} --core

# Restart
restart:
    @echo "Restarting anki-sync-server..."
    kubectl rollout restart -n {{NAMESPACE}} deployment/anki-sync-server
