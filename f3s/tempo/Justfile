NAMESPACE := "monitoring"
APP_NAME := "tempo"

status:
    @echo "=== Pods ==="
    @kubectl get pods -n {{NAMESPACE}} -l app.kubernetes.io/name=tempo
    @echo ""
    @echo "=== Service ==="
    @kubectl get svc -n {{NAMESPACE}} tempo
    @echo ""
    @echo "=== PVC ==="
    @kubectl get pvc -n {{NAMESPACE}} | grep tempo
    @echo ""
    @echo "=== ArgoCD Status ==="
    @kubectl get application {{APP_NAME}} -n cicd -o jsonpath='Sync: {.status.sync.status}, Health: {.status.health.status}' 2>/dev/null && echo ""

logs lines="100":
    kubectl logs -n {{NAMESPACE}} -l app.kubernetes.io/name=tempo --tail={{lines}} -f

port-forward port="3200":
    @echo "Forwarding Tempo to localhost:{{port}}"
    kubectl port-forward -n {{NAMESPACE}} svc/tempo {{port}}:3200

port-forward-otlp-grpc port="4317":
    @echo "Forwarding Tempo OTLP gRPC to localhost:{{port}}"
    kubectl port-forward -n {{NAMESPACE}} svc/tempo {{port}}:4317

port-forward-otlp-http port="4318":
    @echo "Forwarding Tempo OTLP HTTP to localhost:{{port}}"
    kubectl port-forward -n {{NAMESPACE}} svc/tempo {{port}}:4318

sync:
    @echo "Triggering ArgoCD sync..."
    @kubectl annotate application {{APP_NAME}} -n cicd argocd.argoproj.io/refresh=normal --overwrite
    @sleep 2
    @kubectl get application {{APP_NAME}} -n cicd -o jsonpath='Sync: {.status.sync.status}, Health: {.status.health.status}' && echo ""

argocd-status:
    argocd app get {{APP_NAME}} --core

restart:
    @echo "Restarting Tempo..."
    kubectl rollout restart -n {{NAMESPACE}} statefulset/tempo

check:
    @echo "Checking Tempo readiness..."
    @kubectl exec -n {{NAMESPACE}} tempo-0 -- wget -qO- http://localhost:3200/ready
    @echo ""
    @echo "Testing OTLP endpoint with tracing-demo:"
    @echo "  curl https://tracing-demo.f3s.buetow.org/api/process"
