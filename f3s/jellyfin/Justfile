NAMESPACE := "services"
APP_NAME := "jellyfin"

status:
    @echo "=== Pods ==="
    @kubectl get pods -n {{NAMESPACE}} | grep jellyfin
    @echo ""
    @echo "=== Services ==="
    @kubectl get svc -n {{NAMESPACE}} | grep jellyfin
    @echo ""
    @echo "=== Ingress ==="
    @kubectl get ingress -n {{NAMESPACE}} jellyfin-ingress
    @echo ""
    @echo "=== PVCs ==="
    @kubectl get pvc -n {{NAMESPACE}} | grep jellyfin
    @echo ""
    @echo "=== ArgoCD Status ==="
    @kubectl get application {{APP_NAME}} -n cicd -o jsonpath='Sync: {.status.sync.status}, Health: {.status.health.status}' 2>/dev/null && echo ""

logs lines="100":
    kubectl logs -n {{NAMESPACE}} -l app=jellyfin-server --tail={{lines}} -f

port-forward port="8096":
    @echo "Forwarding jellyfin to localhost:{{port}}"
    kubectl port-forward -n {{NAMESPACE}} svc/jellyfin-server {{port}}:8096

sync:
    @echo "Triggering ArgoCD sync..."
    @kubectl annotate application {{APP_NAME}} -n cicd argocd.argoproj.io/refresh=normal --overwrite
    @sleep 2
    @kubectl get application {{APP_NAME}} -n cicd -o jsonpath='Sync: {.status.sync.status}, Health: {.status.health.status}' && echo ""

argocd-status:
    argocd app get {{APP_NAME}} --core

restart:
    @echo "Restarting jellyfin server..."
    kubectl rollout restart -n {{NAMESPACE}} deployment/jellyfin-server
