NAMESPACE := "monitoring"
APP_NAME := "pushgateway"

status:
    @echo "=== Pods ==="
    @kubectl get pods -n {{NAMESPACE}} | grep pushgateway
    @echo ""
    @echo "=== Service ==="
    @kubectl get svc -n {{NAMESPACE}} pushgateway
    @echo ""
    @echo "=== ArgoCD Status ==="
    @kubectl get application {{APP_NAME}} -n cicd -o jsonpath='Sync: {.status.sync.status}, Health: {.status.health.status}' 2>/dev/null && echo ""

logs lines="100":
    kubectl logs -n {{NAMESPACE}} -l app=pushgateway --tail={{lines}} -f

port-forward port="9091":
    @echo "Forwarding pushgateway to localhost:{{port}}"
    kubectl port-forward -n {{NAMESPACE}} svc/pushgateway {{port}}:9091

sync:
    @echo "Triggering ArgoCD sync..."
    @kubectl annotate application {{APP_NAME}} -n cicd argocd.argoproj.io/refresh=normal --overwrite
    @sleep 2
    @kubectl get application {{APP_NAME}} -n cicd -o jsonpath='Sync: {.status.sync.status}, Health: {.status.health.status}' && echo ""

argocd-status:
    argocd app get {{APP_NAME}} --core

restart:
    @echo "Restarting pushgateway..."
    kubectl rollout restart -n {{NAMESPACE}} deployment/pushgateway
